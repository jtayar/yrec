
C  SUBROUTINE FOR GENERATING FIXED Z TABLE FROM THE FULL SET OF
C  ALEXANDER OPACITY TABLES.
      SUBROUTINE ALXZTAB(Z)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMX=7)
      PARAMETER (NUMZ=15)
      PARAMETER (NUMXZ=105)
      PARAMETER (NUMT=23)
      PARAMETER (NUMD=17)
      PARAMETER (NUMXT=8)
C     ALEX LOW T OPACITY
      COMMON /GALOT/TGR(NUMT),XXG(NUMX),R0GR(NUMD),ZZG(NUMZ)
      COMMON /ALOT/CAPALEX(NUMXT,NUMT,NUMD),XE,ZE,IX,IT,ID
      COMMON /ALOTALL/CAPTALEX(NUMXZ,NUMT,NUMD)
C     LOCAL ARRAYS
      REAL*8 QR(4),FZ(4)
      SAVE
C LOCATE FOUR NEAREST TABLES IN Z
      DO I=3,NUMZ-1
         IF(Z.LT.ZZG(I))THEN
            IDZ = I - 2
            GOTO 10
         ENDIF
      END DO
      IDZ = NUMZ - 3
 10   CONTINUE
      DO I = 1,4
         QR(I) = ZZG(IDZ+I-1)
      END DO
C GET INTERPOLATION FACTORS FOR Z.
      CALL INTRP2(QR,FZ,Z)
C INTERPOLATE IN Z AT FIXED X.
      DO I = 1,NUMX
C  INDICES FOR TABLES: SETS OF 7 X AT FIXED Z, STARTING FROM
C  Z= 0 AND GOING TO Z = 0.1.
         II1 = I + NUMX*(IDZ-1)
         II2 = II1 + NUMX
         II3 = II2 + NUMX
         II4 = II3 + NUMX
         DO JJ = 1,NUMT
            DO KK = 1,NUMD
               CAPALEX(I,JJ,KK) = FZ(1)*CAPTALEX(II1,JJ,KK)+
     *         FZ(2)*CAPTALEX(II2,JJ,KK) +
     *         FZ(3)*CAPTALEX(II3,JJ,KK) +
     *         FZ(4)*CAPTALEX(II4,JJ,KK)
            END DO
         END DO
      END DO
      ZE = Z
      RETURN
      END

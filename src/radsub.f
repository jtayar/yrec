C
C
C***********************************************************************
      SUBROUTINE RADSUB (T6,DENSITY,MOLES,TMASS)
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER (MX=5,MV=10,NR=77,NT=56)
C       REAL*8 MOLES,K,MOLENAK,NA  ! KC 2025-05-31
      REAL*8 MOLES,MOLENAK
      COMMON/E/ ESACT,EOS(10)
      COMMON/B/ ZZ(MX),IRI(10),INDEX(10),NTA(NR)

C       DATA NA/6.0221367D+23/, K/1.38065D-16/, UNITF/0.9648575D0/,
C      X UNITFOLD/0.9652D0/, C/2.9979245D+10/, SIGMA/5.67051D-5/,
C      X SIGMAC/1.8914785D-15/,SIGMACC/1.8914785D-3/,APROP/83.1446304D0/
      DATA UNITF/0.9648575D0/,UNITFOLD/0.9652D0/,
     X SIGMACC/1.8914785D-3/,APROP/83.1446304D0/
      SAVE

CPHYSICAL CONSTANTS
C       NA=6.0221367D+23
C       K =1.380658D-16 !   ERG/DEGREE K
C       NA*K=6.0221367E+23*1.380658E-16 ERG/DEGREE K=8.314511E+7 ERG/DEGREE K
C           =8.314511E+7*11604.5 ERG/EV=0.9648575E+12 ERG/EV
C           DEFINE UNITF= NA*K/E+12=0.9648575
C           UNITF=0.9648575  ! OBTAINED WITH LATEST PHYSICAL CONSTANTS
C           UNITFOLD=0.9652   ! OLD UNITS- STILL USED IN THE EOS CODE
C           IN THESE UNITS ENERGY/DENSITY IS IN UNITS OF MB-CC/GM
C           PRESSURE IS IN UNITS OF E+12 BARS=MB
C       SIGMA IS THE STEFAN-BOLTZMANN CONSTANT
C       SIGMA=5.67051E-5 !   ERG /(S*CM**2*K**4)
C       C=2.99792458E+10 !   CM/SEC

C     RAT=SIGMA/C    ! DYNE/(CM**2*K**4)

C     RAT=RAT*1.D+24  !CONVERT DEGREES K TO UNITS 10**6 K (T REPLACED WITH T6)
      RAT=SIGMACC

      PR=4.D0/3.D0*RAT*T6**4   ! MB
      ER=3.D0*PR/DENSITY   ! MB-CC/GM
      SR=4.D0/3.D0*ER/T6   ! MB-CC/(GM-UNIT T6)
      PT=EOS(IRI(1))+PR
      ET=EOS(IRI(2))+ER
      ST=EOS(IRI(3))+SR
      CHIR=EOS(IRI(6))*EOS(IRI(1))/PT
      CHITT=(EOS(IRI(1))*EOS(IRI(7))+4.D0*PR)/PT
C     GAM1T(JCS,I)=(P(JCS,I)*GAM1(JCS,I)+4.D0/3.D0*PR)/PT(JCS,I)
C     GAM2PT(JCS,I)=(GAM2P(JCS,I)*P(JCS,I)+4.D0*PR)/PT(JCS,I)
C     GAM3PT(JCS,I)=GAM1T(JCS,I)/GAM2PT(JCS,I)
      MOLENAK=MOLES*APROP  ! MB-CC/UNIT T6
      CVTT=(EOS(IRI(5))*MOLENAK/TMASS+4.D0*ER/T6)
      GAM3PT=PT*CHITT/(CVTT*DENSITY*T6)
      GAM1T=CHIR+CHITT*GAM3PT
      GAM2PT=GAM1T/GAM3PT

C     NORMALIZE CVT TO 3/2 WHEN GAS IS IDEAL,NON-DEGENERATE,
C     FULLY-IONIZED, AND HAS NO RADIATION CORRECTION
C     CVT=(EOS(5)*MOLENAK/TMASS+4.*ER/T6)
C    X  /MOLENAK
      DEDRHOAT=EOS(IRI(4))-ER/DENSITY
      REVISE=UNITF/UNITFOLD
      PT=PT*REVISE
C MHP 10/02 EN is never used; should this be ET=ET*REVISE???
C      EN=EN*REVISE
      ST=ST*REVISE
C DEDRHOA is never used; should this be DEDRHOAT=DEDRHOAT*REVISE????
C      DEDRHOA=DEDRHOA*REVISE
      EOS(IRI(1))=PT
      EOS(IRI(2))=ET
      EOS(IRI(3))=ST
      EOS(IRI(4))=DEDRHOAT
      EOS(IRI(5))=CVTT
      EOS(IRI(6))=CHIR
      EOS(IRI(7))=CHITT
      EOS(IRI(8))=GAM1T
      EOS(IRI(9))=GAM2PT
      EOS(IRI(10))=GAM3PT
      RETURN
      END

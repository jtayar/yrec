C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C YLLO2D
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE YLLO2D(T,D,M1,M2,M3,O,OL,QODF,QOTF)
C  TWO DIMENSIONAL INTERPOLATION FOR OPACITY
C   AND THE DERIVTIVES.
C M1, M2, AND M3 ARE NEAREST GRID POINT OF ABUNDANCE, TEMPERATURE,
C AND DENSITY WE GOT AT 'GETD'.
C O IS OPACITY.
C OL IS DLOG(O)
C QODF IS THE PARTIAL DERIVATIVE OF O WRT D.
C QOTF IS THE PARTIAL DERIVATIVE OF O WRT T.
C =========================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4 (L)
      PARAMETER (NUMT=50)
      PARAMETER (NUMD=17)
      PARAMETER (NUMX=3)
      PARAMETER (NUMXT=NUMT*NUMX)
      PARAMETER (NUM4D=4*NUMD)
      COMMON /GLLOT/GRIDT(NUMT),GRIDX(NUMX),GRHOT3(NUMD)
      COMMON /LLOT/OPACITY(NUMXT,NUMD),NUMXM,NUMTM
      COMMON /LLOT4/XENV4,ZENV4,CFORD4(NUMT,NUM4D),KIPM1
      COMMON /LINTPL/ CFORD(NUMXT,NUM4D),NDS(NUMXT),NDD(NUMXT)
      REAL*8 XT(NUMT),YTO(NUMT)
      REAL*8 AQOD(NUMT)
      INTEGER JT,IT,ITS,ITF
      LOGICAL*4 LMORE
      SAVE

      LMORE=.TRUE.
C FOR SIX GRID POINTS OF TEMPERATURE
      ITS=M2-2
      IF(ITS.LE.0)ITS=1
      ITF=M2+3
      IF(ITF.GT.NUMTM)ITF=NUMTM
      IF(M1.LT.4)THEN
         MM1=M1
      ELSE
         MM1=KIPM1
      ENDIF
      JT=0
      DO 300 IT=ITS,ITF
         INDEX1=IT+(MM1-1)*NUMTM
         NDSS=NDS(INDEX1)
         IF(NDSS.NE.1)STOP ' OPAL95 2D CHECK NDSS '
         NDF=NDSS+NDD(INDEX1)-1
         CALL FINDEX(GRHOT3,NDF,D,M3)
         IF(M3.LT.0)THEN
C  OUT SIDE THEN  LINEAR EXTRAPOLATION
            M3=-M3
            KNOT=M3-NDSS+1
            INDEX2=4*(KNOT-1)
            DX=D-GRHOT3(M3)
            IF(M1.LT.4)THEN
               C1=CFORD(INDEX1,INDEX2+1)
               C2=CFORD(INDEX1,INDEX2+2)
            ELSE
               C1=CFORD4(IT,INDEX2+1)
               C2=CFORD4(IT,INDEX2+2)
            ENDIF
            OL0=C2*DX+C1
            QODI=C2
         ELSE
C  IN SIDE THEN  SPLINE INTERPOLATION
            KNOT=M3-NDSS+1
            INDEX2=4*(KNOT-1)
            DX=D-GRHOT3(M3)
            IF(M1.LT.4)THEN
               C1=CFORD(INDEX1,INDEX2+1)
               C2=CFORD(INDEX1,INDEX2+2)
               C3=CFORD(INDEX1,INDEX2+3)
               C4=CFORD(INDEX1,INDEX2+4)
            ELSE
               C1=CFORD4(IT,INDEX2+1)
               C2=CFORD4(IT,INDEX2+2)
               C3=CFORD4(IT,INDEX2+3)
               C4=CFORD4(IT,INDEX2+4)
            ENDIF
C INTERPOLATION FOR OPACITY(OL) IN THE ENTRY D AND THE EACH T-GRID
C ESTIMATES THE PARTIAL DERIVATIVE OF OL WRT D
C EVALUATES THE INTERPOLATION VALUE IN THE SUB-RANGE WE DETERMINED.
            OL0=((C4*DX+C3)*DX+C2)*DX+C1
            QODI=(3.0D0*C4*DX+2.0D0*C3)*DX+C2
         ENDIF
C
         JT=JT+1
         XT(JT)=GRIDT(IT)
         YTO(JT)=OL0
         AQOD(JT)=QODI
 300  CONTINUE
      IF(XT(1).GT.T.OR.XT(JT).LT.T) STOP ' EXTRAPOLATION FAILS '
CC INTERPOLATION FOR THE OPACITY IN THE ENTRY T AND D.
CC GET THE PARTIAL DERIVATIVE OF OL WRT T.
      CALL INTPOL(XT,YTO,JT,T,OL00,QOTF)
      OL=OL00
      O=10.0D0**OL
C QOTF = D LN(O)/D LN(T)
CC FIND THE PARTIAL DERIVATIVE VALUE OF OL WRT D IN THE GIVEN T AND D
      CALL INTPOL(XT,AQOD,JT,T,QODF,QODTI)
C QODF = D LN(O)/D LN(D)

      RETURN
      END

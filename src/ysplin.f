
C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C YPSLIN
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C FIND THE COEFFICIENTS FOR THE NATURAL CUBIC SPLINE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C YCK 3/91
      SUBROUTINE YSPLIN(XI,C,N)

      PARAMETER (NP=100)
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 C(4,NP),XI(N),F(NP),H(NP),D(NP),G,D3
      REAL*8 CONST1,CONST2,CONST3,CONST4
      SAVE

C SET THE DIVIDED DIFFERENCE AT EACH SUBINTERVAL.
      DO 10 I=2,N
         H(I)=XI(I)-XI(I-1)
         F(I)=(C(1,I)-C(1,I-1))/H(I)
 10   CONTINUE
C SET THE TRIDIAGONALLY DOMINANT MATRIX AND THE MATRIX EQUATION
C  FOR THE NATURAL SPLINE.
CCCCC FIND THE COEFFICIENTS FOR THE SECOND ORDER TERMS CCCCC
      I=2
      CONST1=3.0D0
      CONST2=1.5D0
      CONST3=2.0D0
      CONST4=1.5D0
      C(2,I)=CONST1*H(I)*F(I+1)+CONST2*H(I+1)*F(I)
      D(I)=CONST3*H(I)+CONST4*H(I+1)
      DO 20 I=3,N-2
         CONST1=3.0D0
         CONST2=3.0D0
         CONST3=2.0D0
         CONST4=2.0D0
         C(2,I)=CONST1*H(I)*F(I+1)+CONST2*H(I+1)*F(I)
         D(I)=CONST3*H(I)+CONST4*H(I+1)
 20   CONTINUE
      I=N-1
      CONST1=1.5D0
      CONST2=3.0D0
      CONST3=1.5D0
      CONST4=2.0D0
      C(2,I)=CONST1*H(I)*F(I+1)+CONST2*H(I+1)*F(I)
      D(I)=CONST3*H(I)+CONST4*H(I+1)
C SOLVE THE MATRIX EQUATION WITH GAUSS METHOD
C ELIMINATION OF THE SUB-DIAGONAL
      DO 30 I=2,N-2
         G=H(I+2)/D(I)
         D(I+1)=D(I+1)-G*H(I)
         C(2,I+1)=C(2,I+1)-G*C(2,I)
 30   CONTINUE
C ELIMINATION OF THE SUPER-DIAGONAL
      C(2,N-1)=C(2,N-1)/D(N-1)
      DO 40 I=N-2,2,-1
         C(2,I)=(C(2,I)-H(I)*C(2,I+1))/D(I)
 40   CONTINUE
C TREATMENT FOR THE FIRST AND LAST ROW.
      C(2,1)=1.5D0*F(2)-C(2,2)/2.0D0
      C(2,N)=1.5D0*F(N)-C(2,N-1)/2.0D0
CCC NOW, WE HAVE THE COEFFICIENTS FOR THE FIRST ORDER TERMS
C
C FIND THE COEFFICIENTS FOR THE SECOND, AND THE THIRD ORDER TERMS
      DO 50 I=1,N-1
         D3=C(2,I)+C(2,I+1)-2.0D0*F(I+1)
         C(3,I)=(F(I+1)-C(2,I)-D3)/H(I+1)
         C(4,I)=D3/(H(I+1)*H(I+1))
 50   CONTINUE
CCC   NOW, WE HAVE COMPLETE SET OF COEFFICIENTS FOR EACH SUB-INTERVAL.

      RETURN
      END

C $$$$$$
      SUBROUTINE WCZ(HD,HJM,HR,HS,HS2,ISTART,IEND,ETA2,HI,OMEGA,
     *               QIW,R0,M)

      use params, only : json
      use settings, only : CLN  ! COMMON/CONST1/

      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      DIMENSION HD(JSON),HJM(JSON),HR(JSON),HS(JSON),HS2(JSON),
     *  ETA2(JSON),HI(JSON),OMEGA(JSON),QIW(JSON),R0(JSON)
      SAVE

      IF(WALPCZ.GE.0.0D0)THEN
C  SOLID BODY ROTATION IN CONVECTIVE REGIONS.
         CALL SOLID(HD,HJM,HR,HS,HS2,ISTART,IEND,ETA2,HI,OMEGA,
     *              QIW,R0,M)
      ELSE IF(WALPCZ.LE.-2.0D0)THEN
C  CONSTANT SPECIFIC ANGULAR MOMENTUM PER UNIT MASS IN THE C.Z.
C  FIND TOTAL MASS AND ANGULAR MOMENTUM OF C.Z.
         CJTOT = HJM(ISTART)*HS2(ISTART)
         CMTOT = HS2(ISTART)
         DO 30 J = ISTART+1,IEND
            CJTOT = CJTOT + HJM(J)*HS2(J)
            CMTOT = CMTOT + HS2(J)
   30    CONTINUE
C  ASSIGN NEW RUN OF J/M IN THE C.Z. AND FIND THE NEW RUN OF OMEGA.
         CJNEW = CJTOT/CMTOT
         DO 40 J = ISTART,IEND
            HJM(J) = CJNEW
            OMEGA(J) = CJNEW*HS2(J)/HI(J)
            JSTART = J
            JEND = J
            CALL SOLID(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,
     *                 OMEGA,QIW,R0,M)
   40    CONTINUE
      ELSE
C  GENERAL LAW FOR OMEGA IN C.Z.: OMEGA = C*R**WALPCZ,WHERE C IS A CONSTANT
C  FOR THE ENTIRE C.Z. IF THIS HOLDS, THE RUN OF J/M CAN BE FOUND BY
C  SOLVING FOR THE CONSTANT BY REQUIRING THAT THE TOTAL J OF THE C.Z. BE
C  CONSERVED AND THEN USING J/M=C*R**WALPCZ*(MOMENT OF INERTIA PER UNIT
C  MASS).
C  FIND TOTAL MASS AND ANGULAR MOMENTUM OF C.Z.
         CJTOT = HJM(ISTART)*HS2(ISTART)
         CMTOT = DEXP(CLN*WALPCZ*HR(ISTART))*HI(ISTART)
         DO 50 J = ISTART+1,IEND
            CJTOT = CJTOT + HJM(J)*HS2(J)
            CMTOT = CMTOT + DEXP(CLN*WALPCZ*HR(J))*HI(J)
   50    CONTINUE
C  ASSIGN NEW RUN OF J/M IN THE C.Z. AND FIND THE NEW RUN OF OMEGA.
         W0 = CJTOT/CMTOT
         DO 60 J = ISTART,IEND
            OMEGA(J) = W0*DEXP(CLN*WALPCZ*HR(J))
            HJM(J) = OMEGA(J)*HI(J)/HS2(J)
            JSTART = J
            JEND = J
            CALL SOLID(HD,HJM,HR,HS,HS2,JSTART,JEND,ETA2,HI,
     *                 OMEGA,QIW,R0,M)
   60    CONTINUE
      ENDIF
      RETURN
      END

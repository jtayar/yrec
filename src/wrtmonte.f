C      SUBROUTINE FOR WRITING MONTE CARLO OUTPUT
      SUBROUTINE WRTMONTE(HCOMP,HD,HL,HP,HR,HS,HT,LC,M,MODEL,DAGE,
     *DDAGE,SMASS,TEFFL,BL,GL,LSHELL,JXBEG,JXMID,JXEND,JCORE,JENV,
     *TLUMX,TRIT,TRIL,PS,TS,RS,CFENV,FTRI,HSTOT,OMEGA,RLL,ICONV,NK,NN)
      use params, only : json
      use settings, only : IMONTE1, IMONTE2  ! COMMON/IOMONTE/
      use settings, only : RESCAL  ! COMMON/CKIND/

      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON /NEWXYM/XENV0A(50),ZENV0A(50),CMIXLA(50),LSENV0A(50),
     * SENV0A(50)
      COMMON/CALSUN/DLDX,DRDX,DLDA,DRDA,BLP,RLP,DX,DA,LSOL
      COMMON/FLUXES/FLUX(10),FLUXTOT(10),CLSNU,GASNU
      COMMON/MONTE2/S11(1000),S33(1000),S34(1000),S17(1000),
     *              ZX(1000),FHE(1000),FZDIF(1000),FLUM(1000),
     *              FAGE(1000)
C MHP 3/96 CENTRAL T,P,RHO ADDED
      COMMON/CENT/TCENTER,PCENTER,DCENTER,ENVLM,ENVR
      DIMENSION TLUMX(8),TRIL(3),TRIT(3),PS(3),TS(3),RS(3),CFENV(9),
     *HS(JSON),HL(JSON),HR(JSON),HP(JSON),HT(JSON),HD(JSON),LC(JSON),
     *HCOMP(15,JSON),OMEGA(JSON)
      SAVE
C  STORE LAST CONVERGED MODEL IN LOGICAL UNIT IMONTE2
      IWRITE = IMONTE2
      CALL WRTLST(IWRITE,HCOMP,HD,HL,HP,HR,HS,HT,LC,TRIT,TRIL,PS,
     *TS,RS,CFENV,FTRI,TLUMX,JCORE,JENV,NN,M,SMASS,TEFFL,BL,HSTOT,
     *DAGE,DDAGE,OMEGA)
C GLOBAL INFORMATION SENT TO FIRST MONTE CARLO OUTPUT FILE
C SURFACE Z/X
      ZTOX = HCOMP(3,M)/HCOMP(1,M)
C HEADER FILE:  MONTE CARLO PARAMETERS
      WRITE(IMONTE1,10)NN,S11(NN),S33(NN),S34(NN),S17(NN),
     *        ZX(NN),FHE(NN),FZDIF(NN),FLUM(NN),FAGE(NN)
   10 FORMAT(I7,1P9E10.3)
C NUMERICAL DATA : #OF RUNS NEEDED FOR A CONVERGED MODEL, INITIAL X
C AND ALPHA, FINAL DL/DX,DR/DX,DL/D ALPHA, DR/D ALPHA
      WRITE(IMONTE1,20)ICONV,DLDX,DRDX,DLDA,DRDA
C      WRITE(IMONTE1,20)ICONV,XGUESS,AGUESS,DLDX,DRDX,DLDA,DRDA
C 20   FORMAT(1X,I2,2F10.6,1P4E11.4)
 20   FORMAT(1X,I2,1P4E11.4)
C NEUTRINO FLUXES (SEE ENGEB FOR DETAILS)
      WRITE(IMONTE1,30) CLSNU,GASNU,(FLUXTOT(I),I=1,8)
 30   FORMAT(1X,2F8.3,1P8E10.3)
C SUMMARY OF STRUCTURE : TC, RHOC, PC, XC, ZC (ADD MU C)
      TCEN = 10.0D0**(TCENTER-6.0D0)
      PCEN = 10.0D0**(PCENTER-17.0D0)
      DCEN = 10.0D0**DCENTER
      WRITE(IMONTE1,40)TCEN,DCEN,PCEN,HCOMP(1,1),HCOMP(3,1)
 40   FORMAT(1X,F7.3,F7.2,F6.3,2F8.5)
C  #SHELLS, INITIAL ALPHA, Y, Z; FINAL R, L
      YINI = 1.0D0 - RESCAL(2,NK-2) - RESCAL(3,NK-2)
      ZINI = RESCAL(3,NK-2)
      WRITE(IMONTE1,50)M,CMIXLA(NK),YINI,ZINI,BL,RLL
 50   FORMAT(I5,F7.4,2F8.5,1P2E10.3)
C  CZ DEPTH (R,M), SURFACE Y, Z, Z/X (ADD T CZ BASE, RHO CZ BASE)
      WRITE(IMONTE1,60)ENVR,ENVLM,HCOMP(2,M),HCOMP(3,M),ZTOX
 60   FORMAT(F8.5,F9.6,2F8.5,F9.6)
C  ENERGY GENERATION FRACTIONS PP I,II,III,CNO,EGRAV
      WRITE(IMONTE1,70)(TLUMX(J),J=1,4),TLUMX(7)
 70   FORMAT(1P5E10.3)
      RETURN
      END

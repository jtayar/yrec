      FUNCTION SPLINE(XVALS,Z1,Z2,XTABS,YTABS,XTABS1,YTABS1,
     *                Y1,Y2,E2,W2,V2,NCASE)
C
C                                 SHAPE PRESERVING QUDRATIC SPLINES
C                                   BY D.F.MCALLISTER & J.A.ROULIER
C                                     CODED BY S.L.DODD & M.ROULIER
C                                       N.C. STATE UNIVERSITY
C
      REAL*8 XVALS,Z1,Z2,XTABS,YTABS,XTABS1,YTABS1,V2,
     *     W2,Y1,Y2,E2,SPLINE,FS
C
C SPLINE FINDS THE IMAGE OF A POINT IN XVAL.
C
C ON INPUT--
C
C   XVALS CONTAINS THE VALUE AT WHICH THE SPLINE IS EVALUATED.
C
C   (XTABS,YTABS) ARE THE COORDINATES OF THE LEFT-HAND DATA POINT
C   USED IN THE EVALUATION OF XVALS.
C
C   (XTABS1,YTABS1) ARE THE COORDINATES OF THE RIGHT-HAND DATA POINT
C   USED IN THE EVALUATION OF XVALS.
C
C   Z1,Z2,Y1,Y2,E2,W2,V2 ARE THE PARAMETERS OF THE SPLINE.
C
C   NCASE CONTROLS THE EVALUATION OF THE SPLINE BY INDICATING WHETHER
C   ONE OR TWO KNOTS WERE PLACED IN THE INTERVAL (XTABS,XTABS1).
C
C
C ON OUTPUT--
C
C   SPLINE IS THE IMAGE OF XVALS.
C
C AND
C
C   SPLINE DOES NOT ALTER ANY OF THE INPUT PARAMETERS.
C
C----------------------------------------------------------------------
C
C  *****MODIFICATION DUE TO MARC PINSONNEAULT 6/87*****
C  IF DIVISION BY ZERO WOULD BE CAUSED,LINEAR INTERPOLATION IS USED
C  INSTEAD OF THE SPLINE.
C IF NCASE .EQ. 4, MORE THAN ONE KNOT WAS PLACED IN THE INTERVAL.
      save

      IF (NCASE .EQ. 4) GO TO 40
C
C CASES 1,2, OR 3.
C
C DETERMINE THE LOCATION OF XVALS RELATIVE TO THE KNOT.
C KC 2025-05-30 fixed "Arithmetic IF statement"
C       IF (Z1 - XVALS) 10,20,30
      IF (Z1 .LT. XVALS) THEN
         GOTO 10
      ELSE IF (Z1 .EQ. XVALS) THEN
         GOTO 20
      ELSE
         GOTO 30
      END IF
C
  10  CONTINUE
      IF(XTABS1.NE.Z1)THEN
      SPLINE=(Z2*(XTABS1-XVALS)**2+W2*2.D0*(XVALS-Z1)*(XTABS1-XVALS)
     *        +YTABS1*(XVALS-Z1)**2)/(XTABS1-Z1)**2
      ELSE
       FS = (XVALS - XTABS)/(XTABS1 - XTABS)
       SPLINE = YTABS + FS*(YTABS1 - YTABS)
      ENDIF
      RETURN
C
  20  SPLINE=Z2
      RETURN
C
  30  CONTINUE
      IF(Z1.NE.XTABS)THEN
      SPLINE=(YTABS*(Z1-XVALS)**2+V2*2.D0*(XVALS-XTABS)*(Z1-XVALS)+
     *        Z2*(XVALS-XTABS)**2)/(Z1-XTABS)**2
      ELSE
       FS = (XVALS - XTABS)/(XTABS1 - XTABS)
       SPLINE = YTABS + FS*(YTABS1 - YTABS)
      ENDIF
      RETURN
C
C CASE 4.
C
C DETERMINE THE LOCATION OF XVALS RELATIVE TO THE FIRST KNOT.
C KC 2025-05-30 fixed "Arithmetic IF statement"
C   40  IF (Y1 - XVALS) 70,60,50
  40  IF (Y1 .LT. XVALS) THEN
         GOTO 70
      ELSE IF (Y1 .EQ. XVALS) THEN
         GOTO 60
      ELSE
         GOTO 50
      END IF
C
  50  CONTINUE
      IF(Y1.NE.XTABS)THEN
      SPLINE=(YTABS*(Y1-XVALS)**2+V2*2.D0*(XVALS-XTABS)*(Y1-XVALS)+
     *        Y2*(XVALS-XTABS)**2)/(Y1-XTABS)**2
      ELSE
       FS = (XVALS - XTABS)/(XTABS1 - XTABS)
       SPLINE = YTABS + FS*(YTABS1 - YTABS)
      ENDIF
      RETURN
C
  60  SPLINE=Y2
      RETURN
C
C DETERMINE THE LOCATION OF XVALS RELATIVE TO THE SECOND KNOT.
C KC 2025-05-30 fixed "Arithmetic IF statement"
C   70  IF (Z1 - XVALS) 100,90,80
  70  IF (Z1 .LT. XVALS) THEN
         GOTO 100
      ELSE IF (Z1 .EQ. XVALS) THEN
         GOTO 90
      ELSE
         GOTO 80
      END IF
C
  80  CONTINUE
      IF(Z1.NE.Y1)THEN
      SPLINE=(Y2*(Z1-XVALS)**2+E2*2.D0*(XVALS-Y1)*(Z1-XVALS)+Z2*(XVALS
     *        -Y1)**2)/(Z1-Y1)**2
      ELSE
       FS = (XVALS - XTABS)/(XTABS1 - XTABS)
       SPLINE = YTABS + FS*(YTABS1 - YTABS)
      ENDIF
      RETURN
C
  90  SPLINE=Z2
      RETURN
C
  100 CONTINUE
      IF(XTABS1.NE.Z1)THEN
      SPLINE=(Z2*(XTABS1-XVALS)**2+W2*2.D0*(XVALS-Z1)*(XTABS1-XVALS)
     *        +YTABS1*(XVALS-Z1)**2)/(XTABS1-Z1)**2
      ELSE
       FS = (XVALS - XTABS)/(XTABS1 - XTABS)
       SPLINE = YTABS + FS*(YTABS1 - YTABS)
      ENDIF
      RETURN
      END

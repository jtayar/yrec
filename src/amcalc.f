C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C AMCALC
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$

      SUBROUTINE AMCALC(SMASS,BL,TEFFL)
      use parmin90, only : CLSUNL, CRSUN  ! COMMON/CONST/
      use parmin90, only : CLN, C4PIL  ! COMMON/CONST1/
      use parmin90, only : CSIGL  ! COMMON/CONST2/

      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      IMPLICIT INTEGER*4(I,J,K,M,N)

C MHP 8/17 ADDED EXCEN, C_2 TO COMMON BLOCK FOR MATT ET AL. 2012 CENT. TERM
      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,EXL,EXPR,CONSTFACTOR,
     *             STRUCTFACTOR,EXCEN,C_2,LJDOT0
C      COMMON/CWIND/WMAX,EXMD,EXW,EXTAU,EXR,EXM,CONSTFACTOR,STRUCTFACTOR,LJDOT0
      CHARACTER*3 AWIND
      COMMON/PMMWIND/AWIND,LMWIND,LROSSBY,LBSCALE,PMMA,PMMB,PMMC,PMMD,PMMM,
     *         PMMJD,PMMMD,PMMSOLP,PMMSOLW,PMMSOLTAU
      COMMON/OVRTRN/LNEWTCZ,LCALCENV,TAUCZ,TAUCZ0,PPHOT,PPHOT0,FRACSTEP
      SAVE

C G Somers, 6/16
C THIS SHORT SUBROUTINE WILL USE THE DELIVERED STRUCTURAL VARIABLES TO DETERMINE
C THE STRENGTH OF THE TORQUE, ACCORDING TO THE MATT ET AL. (2012) FORMULATION.
C
C COLLECT THE RELEVANT STRUCTURE VARIABLES, AND 
C     MASS
      RMASS = SMASS
C     RADIUS
      RL = 0.5D0*(BL+CLSUNL-C4PIL-CSIGL-4.D0*TEFFL)
      RTOT  = DEXP(CLN*RL)
      RRAD = RTOT/CRSUN
C     LUMINOSITY
      RLUM = 10.**BL
C     PHOTOSPHERIC PRESSURE
      RPHOT = 10.**(PPHOT0+FRACSTEP*(PPHOT-PPHOT0))/(10.**PMMSOLP)
C     CONVECTIVE OVERTURN TIMESCALE
      IF(LROSSBY)THEN
         RTAU = (TAUCZ0+FRACSTEP*(TAUCZ-TAUCZ0))/PMMSOLTAU
      ELSE
         RTAU = 1.
      ENDIF
C     COMBINE THEM ALL
      STRUCTFACTOR = RMASS**EXM * RRAD**EXR * RLUM**EXL * RPHOT**EXPR 
     *               * RTAU**EXTAU
      RETURN
      END

C
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     OP95ZTAB
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      SUBROUTINE OP95ZTAB(Z)
c MHP 7/98 COMPUTE OPACITY TABLE AT FIXED Z FROM THE FULL OP95 SET
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      PARAMETER (NUMT=70)
      PARAMETER (NUMD=19)
      PARAMETER (NUMX=10)
      PARAMETER (NUMZ=13)
      PARAMETER (NUMXZ=126)
C GRID ENTRIES FOR TEMPERATURE, ABUNDANCE (X), AND RHO/T6**3
C OPACITY AS A FUNCTION OF X, T, AND RHO/T6**3
      COMMON /LLOT95/CAPPA(NUMX,NUMT,NUMD),ZTAB
C FULL SET OF TABLES: OPACITY AS A FUNCTION OF Z AND X, T, RHO/T6**3
C TABLES ARE INCREMENTED IN SETS OF NZ*NX.  SO THE TABLES FOR THE
C THIRD METAL ABUNDANCE (3 X 10**-4)BEGIN AT TABLE 21 AND END AT TABLE 30.
C FOR THE HIGH VALUES OF Z, THE NUMBER OF X TABLES IS NOT THE SAME (I.E.
C X<0.9 FOR Z=0.1).
C FOR EACH COMPOSITION A FULL GRID IN (T,RHO/T6**3) IS RETAINED.
      COMMON /LLOT95A/TGR(NUMT),XXG(NUMX),R0GR(NUMD),ZZG(NUMZ),
     *                CAPPA2(NUMXZ,NUMT,NUMD),NUMXM(NUMZ),NZ(NUMZ)
      DIMENSION A(4),B(4),BB(4)
      SAVE
      ZTAB = Z
C  FIND 4 NEAREST TABLES IN Z TO DESIRED VALUE.
      DO I = 3,NUMZ-1
         IF(ZZG(I).GE.Z)THEN
            IDZ = I - 2
            GOTO 10
         ENDIF
      END DO
C DESIRED Z > 0.1D0; STOP
      WRITE(*,5)Z
 5    FORMAT(1X,'DESIRED Z',F10.6,'OUTSIDE OP95 TABLE RANGE'/
     *       ' RUN STOPPED')
      STOP
 10   CONTINUE
C  FIND INTERPOLATION FACTORS IN Z.
      DO I = 1,4
         A(I) = ZZG(IDZ+I-1)
      END DO
      CALL INTRP2(A,B,Z)
C  FOR X TABLES 1-8, ALL VALUES OF Z PRESENT; PERFORM INTERPOLATION.
      DO I = 1,8
C INDICES FOR 4 DESIRED COMPOSITIONS.
         I1 = NZ(IDZ)+I
         I2 = NZ(IDZ+1)+I
         I3 = NZ(IDZ+2)+I
         I4 = NZ(IDZ+3)+I
         DO J = 1,NUMT
            DO K = 1,NUMD
               CAPPA(I,J,K) = B(1)*CAPPA2(I1,J,K)+
     *         B(2)*CAPPA2(I2,J,K)+B(3)*CAPPA2(I3,J,K)+
     *         B(4)*CAPPA2(I4,J,K)
            END DO
         END DO
      END DO
C  FOR X TABLES 9 AND 10, HIGH VALUES OF Z ARE NOT PRESENT
C  OMIT TABLE 9 (X=0.95) IF DESIRED Z > 0.04
      IF(Z.GE.0.04D0)GOTO 20
C  OTHERWISE, CHECK TO ENSURE THAT THE 4 Z TABLES USED HAVE Z < 0.04
C  ADJUST INTERPOLATION FACTORS IF NEEDED
      IF(ZZG(IDZ+3).GT.0.04D0)THEN
         IDZ2 = NUMZ - 6
         DO I = 1,4
            A(I) = ZZG(IDZ2+I-1)
         END DO
         CALL INTRP2(A,BB,Z)
      ELSE
         IDZ2 = IDZ
         DO I = 1,4
            BB(I) = B(I)
         END DO
      ENDIF
      I1 = NZ(IDZ2)+9
      I2 = NZ(IDZ2+1)+9
      I3 = NZ(IDZ2+2)+9
      I4 = NZ(IDZ2+3)+9
      DO J = 1,NUMT
         DO K = 1,NUMD
            CAPPA(9,J,K) = BB(1)*CAPPA2(I1,J,K)+
     *      BB(2)*CAPPA2(I2,J,K)+BB(3)*CAPPA2(I3,J,K)+
     *      BB(4)*CAPPA2(I4,J,K)
         END DO
      END DO
 20   CONTINUE
C  OMIT TABLE 10 (X = 1-Z) IF DESIRED Z >= 0.1
      IF(Z.GE.0.1D0)GOTO 30
C  CHECK TO ENSURE THAT Z=0.1 TABLE IS NOT ONE OF THE 4 TABLES; ADJUST
C  INTERPOLATION FACTORS IF NEEDED.
      IF(ZZG(IDZ+3).GE.0.1D0)THEN
         IDZ2 = NUMZ - 4
         DO I = 1,4
            A(I) = ZZG(IDZ2+I-1)
         END DO
         CALL INTRP2(A,BB,Z)
      ELSE
         IDZ2 = IDZ
         DO I = 1,4
            BB(I) = B(I)
         END DO
      ENDIF
 30   CONTINUE
      I1 = NZ(IDZ2)+10
      I2 = NZ(IDZ2+1)+10
      I3 = NZ(IDZ2+2)+10
      I4 = NZ(IDZ2+3)+10
      DO J = 1,NUMT
         DO K = 1,NUMD
            CAPPA(10,J,K) = BB(1)*CAPPA2(I1,J,K)+
     *      BB(2)*CAPPA2(I2,J,K)+BB(3)*CAPPA2(I3,J,K)+
     *      BB(4)*CAPPA2(I4,J,K)
         END DO
      END DO
      RETURN
      END

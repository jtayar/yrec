

C
C
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
C MMID
C$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$
      SUBROUTINE MMID(Y,DYDX,NVAR,XS,HTOT,NSTEP,YOUT,DERIV,B,FPL,FTL,
     *          GL,LATMO,LDERIV,LOCOND,LSAVE,RL,TEFFL,X,Z,KOUNT,KSAHA)

      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      DIMENSION Y(3),DYDX(3),YOUT(3),YM(3),YN(3)
      EXTERNAL DERIV
      SAVE

C  INTEGRATES THE DEPENDENT VARIABLES Y FROM XS TO XS + HTOT IN
C  NSTEP INCREMENTS.  INPUT ARE THE Y'S AND DY/DX'S AT XS; VALUES OF
C  Y AT XS + HTOT ARE STORED IN YOUT.  DERIVATIVES ARE CALCULATED IN
C  SUBROUTINE DERIV. THIS SR FROM NUMERICAL RECIPES, P.562.
C  H IS THE SIZE OF EACH SMALL STEP.
      H = HTOT/DFLOAT(NSTEP)
C  FIRST STEP
      DO 10 I = 1,NVAR
       YM(I) = Y(I)
       YN(I) = Y(I) + DYDX(I)*H
   10 CONTINUE
      X0 = XS + H
C  YOUT TEMPORARILY USED FOR STORAGE OF DERIVATIVES.
      CALL DERIV(X0,YN,YOUT,B,FPL,FTL,GL,LATMO,LDERIV,LOCOND,LSAVE,
     *           RL,TEFFL,X,Z,KOUNT,KSAHA)
      H2 = 2.0D0*H
C  GENERAL STEP.
      DO 30 N = 2,NSTEP
       DO 20 I = 1,NVAR
          SWAP = YM(I) + H2*YOUT(I)
          YM(I) = YN(I)
          YN(I) = SWAP
   20    CONTINUE
       X0 = X0 + H
       CALL DERIV(X0,YN,YOUT,B,FPL,FTL,GL,LATMO,LDERIV,LOCOND,LSAVE,
     *              RL,TEFFL,X,Z,KOUNT,KSAHA)
   30 CONTINUE
C  LAST STEP.
      DO 40 I = 1,NVAR
       YOUT(I) = 0.5D0*(YM(I) + YN(I) + H*YOUT(I))
   40 CONTINUE
      RETURN
      END

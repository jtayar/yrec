C $$$$$$
      SUBROUTINE SHAPE(HD,HR,HS,JSTART,JEND,OMEGA,ETA2,R0)

      PARAMETER (JSON=5000)
      IMPLICIT REAL*8(A-H,O-Z)
      IMPLICIT LOGICAL*4(L)
      COMMON/CONST1/ CLN,CLNI,C4PI,C4PIL,C4PI3L,CC13,CC23,CPI
      COMMON/CONST2/CGAS,CA3,CA3L,CSIG,CSIGL,CGL,CMKH,CMKHN
      COMMON/ROT/WNEW,WALPCZ,ACFPFT,ITFP1,ITFP2,LROT,LINSTB,LWNEW
      DIMENSION HD(JSON),HR(JSON),HS(JSON),OMEGA(JSON),ETA2(JSON),
     *R0(JSON)
      SAVE
C  SHAPE FINDS THE DISTORTION OF A GIVEN SHELL FROM SPHERICAL SYMMETRY;
C  IT CALCULATES THE CHARACTERISTIC RADIUS R0 AND ASSOCIATED PARAMETER
C  ETA2.  IN POLAR CO-ORDINATES THE RADIUS OF A SHELL IS
C  R = R0*(1 - A*P2), WHERE A IS A FUNCTION OF ROTATION RATE AND P2 IS
C  THE SECOND LEGENDRE POLYNOMIAL.  FOR MORE INFORMATION SEE ENDAL AND
C  SOFIA.
      CG = DEXP(CLN*CGL)
      C1 = 0.6D0
      C2 = 2.0D0/3.5D1
      C3 = 3.0D0*C1
      C4 = 4.0D0*C2
      JBEGIN = JSTART
      IF(JSTART.EQ.1) THEN
C  CALCULATE FIRST POINT ETA2 USING CENTRAL B.C.
C  NOTE THAT THE SUFFIX 'P' DENOTES A QUANTITY FROM THE PREVIOUS SHELL.
C  E.G. RHOP IS THE DENSITY OF THE LAST SHELL AND RHO IS THE DENSITY OF
C  THE CURRENT ONE.
         RHOBAR = DEXP(CLN*(HS(1) - C4PI3L - 3.0D0*HR(1)))
         RPHI = DEXP(CLN*HR(1))
         RHO = DEXP(CLN*HD(1))
         ETA2(1) = 6.D0*(1.0D0 - RHO/RHOBAR)
C ITERATE FOR R0 GIVEN RPHI AND ETA2
         GM = CG*DEXP(CLN*HS(1))
         RPHI3 = RPHI**3
         R03 = RPHI3
         FACT = 5.0D0*CC13*OMEGA(1)**2/(GM*(2.0D0+ETA2(1)))
         DO 10 J = 1,ITFP2
            A = FACT*R03
            DELR03 = (RPHI3-R03*(1.0D0 + C1*A**2 - C2*A**3))/
     *      (1.0D0 + C3*A**2 - C4*A**3)
            R03 = R03 + DELR03
            IF(DABS(DELR03/R03).LE.ACFPFT)GOTO 20
   10    CONTINUE
   20    R0(1) = R03**CC13
         IF(JEND.EQ.1) GOTO 9999
         RHOBP = RHOBAR
         RPHIP = RPHI
         RHOP = RHO
         JBEGIN = 2
      ELSE
         RHOBP = DEXP(CLN*(HS(JSTART-1) - C4PI3L - 3.0D0*HR(JSTART-1)))
         RPHIP = DEXP(CLN*HR(JSTART-1))
         RHOP = DEXP(CLN*HD(JSTART-1))
      ENDIF
C  CALCULATE ETA2 AND R0 FOR REMAINING POINTS
      DO 100 I = JBEGIN,JEND
         RHOBAR = DEXP(CLN*(HS(I) - C4PI3L - 3.0D0*HR(I)))
         RHO = DEXP(CLN*HD(I))
         RPHI = DEXP(CLN*HR(I))
         GM = CG*DEXP(CLN*HS(I))
         DR = RPHI - RPHIP
C FIND ETA2 USING 4-POINT RUNGE-KUTTE TECHNIQUE
C D(ETA2)/D(R0) IS COMPUTED USING RADAU'S EQUATION:
C R*(DETA2/DR)+6*RHO*(ETA2+1)/RHOBAR+ETA2*(ETA2-1) = 6 ,AND ETA2(0) = 0
C FOR A FIRST GUESS,R0 = RPHI IS ASSUMED.
C    FIRST EVALUATE DETA/DR AT THE BEGINNING OF THE INTERVAL
         DETA1 = DR*(6.0D0 - 6.0D0*RHOP*(ETA2(I-1) + 1.0D0)/RHOBP
     *   - ETA2(I-1)*(ETA2(I-1) - 1.0D0))/RPHIP
C RHOA AND RHOBA ARE AVERAGES OF THE RHO,RHOBAR OF OLD AND NEW SHELLS
         RHOA = 0.5D0*(RHO+RHOP)
         RHOBA = 0.5D0*(RHOBAR+RHOBP)
         ETAA = ETA2(I-1) + 0.5D0*DETA1
         RPHIA = RPHIP + 0.5D0*DR
C    USING THE ESTIMATED ETA2 AT THE MIDPOINT,FIND DETA/DR AT THE
C    MIDPOINT.
         DETA2 = DR*(6.0D0 - 6.0D0*RHOA*(ETAA + 1.0D0)/RHOBA
     *   - ETAA*(ETAA - 1.0D0))/RPHIA
         ETAA = ETA2(I-1) + 0.5D0*DETA2
C    USING THIS REFINED DERIVATIVE TO ESTIMATE ETA2 AT MIDPOINT,
C    FIND DETA/DR AT THE MIDPOINT AGAIN.
         DETA3 = DR*(6.0D0 - 6.0D0*RHOA*(ETAA + 1.0D0)/RHOBA
     *   - ETAA*(ETAA - 1.0D0))/RPHIA
         ETAA = ETA2(I-1) + DETA3
C    USING DETA/DR AT THE MIDPOINT TO ESTIMATE ETA2 AT THE END OF
C    THE INTERVAL,GET DETA/DR AT THE END OF THE INTERVAL.
         DETA4 = DR*(6.0D0 - 6.0D0*RHO*(ETAA + 1.0D0)/RHOBAR
     *   - ETAA*(ETAA - 1.0D0))/RPHI
C    PERFORM 4TH ORDER RUNGE-KUTTE INTEGRATION USING ABOVE 4 DERIVS.
         ETA2(I) = ETA2(I-1)+CC13*(0.5D0*DETA1+DETA2+DETA3+0.5D0*DETA4)
         RPHI3 = RPHI**3
         R03 = RPHI3
         R0EST = RPHI
         ACC = ACFPFT**CC13
C ITERATE BETWEEN SOLUTION FOR ETA2 AND SOLUTION FOR R0 ITFP1 TIMES.
         DO 60 K = 1,ITFP1
            FACT = 5.0D0*CC13*OMEGA(I)**2/(GM*(2.0D0+ETA2(I)))
C NOW ITERATE FOR R0 GIVEN RPHI AND ETA2, USING THE RELATION
C RPHI**3 = R0**3(1.0 + 3/5A**2 - 2/35A**3)
C WHERE A = OMEGA**2*R0**3*5/3GM(2+ETA2))
            DO 40 J = 1,ITFP2
               A = FACT*R03
               DELR03 = (RPHI3-R03*(1.0D0 + C1*A**2 - C2*A**3))/
     *         (1.0D0 + C3*A**2 - C4*A**3)
               R03 = R03 + DELR03
               IF(DABS(DELR03/R03).LT.ACFPFT)GOTO 50
   40       CONTINUE
   50       R0(I) = R03**CC13
            ERR = R0(I) - R0EST
C  ETA2 IS A FUNCTION OF R0, AND R0=RPHI WAS USED TO CALCULATE ETA2
C  CORRECT ETA2 HERE IF DR/R0 > CUBE ROOT OF ACFPFT
            IF(DABS(ERR)/R0(I).LE.ACC) GOTO 70
C FIND ETA2 USING 4-POINT RUNGE-KUTTE TECHNIQUE AGAIN,BUT FINDING
C ETA2 AT R0(I) RATHER THAN ASSUMING R0 = RPHI.
            ETA2T = ETA2(I)
            DR = R0(I) - R0(I-1)
            DETA1 = DR*(6.0D0 - 6.0D0*RHOP*(ETA2(I-1) + 1.0D0)/RHOBP
     *              - ETA2(I-1)*(ETA2(I-1) - 1.0D0))/R0(I-1)
            ETAA = ETA2(I-1) + 0.5D0*DETA1
            R0A = R0(I-1) + 0.5D0*DR
            DETA2 = DR*(6.0D0 - 6.0D0*RHOA*(ETAA + 1.0D0)/RHOBA
     *              - ETAA*(ETAA - 1.0D0))/R0A
            ETAA = ETA2(I-1) + 0.5D0*DETA2
            DETA3 = DR*(6.0D0 - 6.0D0*RHOA*(ETAA + 1.0D0)/RHOBA
     *              - ETAA*(ETAA - 1.0D0))/R0A
            ETAA = ETA2(I-1) + DETA3
            DETA4 = DR*(6.0D0 - 6.0D0*RHO*(ETAA + 1.0D0)/RHOBAR
     *              - ETAA*(ETAA - 1.0D0))/R0(I)
            ETA2(I) = ETA2(I-1) + CC13*(0.5D0*DETA1+DETA2+DETA3
     *                + 0.5D0*DETA4)
            ERR = ETA2T - ETA2(I)
            IF(DABS(ERR).LE.ACC) GOTO 70
            R0EST = R0(I)
   60    CONTINUE
   70    CONTINUE
         RHOP = RHO
         RHOBP = RHOBAR
         RPHIP = RPHI
  100 CONTINUE
 9999 CONTINUE
      RETURN
      END
